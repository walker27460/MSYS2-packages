# Maintainer: Martell Malone <Martell Malone at g mail dot com>

pkgname=ninja
pkgver=1.8.2
pkgrel=1
pkgdesc="Ninja is a small build system with a focus on speed (mingw-w64)"
arch=('any')
url="https://ninja-build.org"
license=('Apache')
depends=()
options=('strip' 'staticlibs')
makedepends=("re2c" "python3")
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/ninja-build/ninja/archive/v${pkgver}.tar.gz"
        0001-Make-TimeStamp-64-bit.patch
        0002-Read-file-timestamps-in-higher-resolution.patch
        0003-Move-include-for-type-definition-to-correct-place.patch
        0004-Add-define-to-get-printf-format-specifiers.patch
        0005-Use-64-bit-alignment-safe-timestamp-reading.patch
        0006-Update-checks-for-new-stat-fields.patch
        0007-Fix-some-Windows-troubles.patch
        0008-Use-strtoll-when-reading-mtime-from-build-log.patch
        0009-Fix-minor-typo.patch)
sha256sums=('86b8700c3d0880c2b44c2ff67ce42774aaf8c28cbf57725cb881569288c1c6f4'
            '6fd02b4f2230a178a70728f2838fd43a29406e03a149589476d7d5123832ae75'
            '259dd5e4d638113157f893ffa187a2ce557611f2bd9ae817d17c3f78692ba9ca'
            '5137444a06a719c6e454ac7ad3681caa0c3858405d3de4690af2017d44111a9c'
            '2a94d63bb899ad7529f880f6999d17908e64bad24bd0e2697a47e1e4ec6fee92'
            '59819629b6b282112a627babab0686e33551dfb53e01486ea5d07dcd529cedbe'
            '5370831124e63be859f6846673be528d658536aff2524c8defe6a707db0bf966'
            '1a370aa0c22d1cd1355622712cd63af8f7742629f4fdd028c32dee2999be335c'
            'd19b4d3b4d4a042ce0bd876973f46c317d242f73e4507baad426f0e23351c7a8'
            'cc991ed0bbccdd8d892961ac7ef6f184dde1faa217073ecbdd775430335e7cbe')

prepare () {
  cd ${srcdir}/ninja-${pkgver}
  patch -p1 -i ${srcdir}/0001-Make-TimeStamp-64-bit.patch
  patch -p1 -i ${srcdir}/0002-Read-file-timestamps-in-higher-resolution.patch
  patch -p1 -i ${srcdir}/0003-Move-include-for-type-definition-to-correct-place.patch
  patch -p1 -i ${srcdir}/0004-Add-define-to-get-printf-format-specifiers.patch
  patch -p1 -i ${srcdir}/0005-Use-64-bit-alignment-safe-timestamp-reading.patch
  patch -p1 -i ${srcdir}/0006-Update-checks-for-new-stat-fields.patch
  patch -p1 -i ${srcdir}/0007-Fix-some-Windows-troubles.patch
  patch -p1 -i ${srcdir}/0008-Use-strtoll-when-reading-mtime-from-build-log.patch
  patch -p1 -i ${srcdir}/0009-Fix-minor-typo.patch
}

build() {
  cd ${srcdir}/ninja-${pkgver}
  /usr/bin/python3 configure.py --bootstrap
}

package() {
  mkdir -p "${pkgdir}"/usr/bin
  mv "${srcdir}/ninja-${pkgver}/ninja.exe" "${pkgdir}"/usr/bin/
  mkdir -p "${pkgdir}"/usr/lib
  mv "${srcdir}/ninja-${pkgver}/build/libninja.a" "${pkgdir}"/usr/lib/
}
