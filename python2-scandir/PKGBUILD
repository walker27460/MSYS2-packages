# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

pkgname=python2-scandir
pkgver=1.9.0
pkgrel=1
pkgdesc="A better directory iterator and faster os.walk()"
arch=('i686' 'x86_64')
url="https://github.com/benhoyt/scandir"
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/benhoyt/scandir/archive/v$pkgver.tar.gz")
sha512sums=('af3f20fe19620a7ccc17b2661dbf2d61038c6c55f1e815bcc777ad3776b469d523bbf0ca1052212ee45d0fd932cd6f055cae3a18592ef8bac2078eeada4ec6f4')

build() {
  cd scandir-$pkgver
  python2 setup.py build
}

check() {
  cd scandir-$pkgver
  LC_CTYPE=en_US.UTF-8 python2 test/run_tests.py || warning "Tests failed"
}

package() {
  cd scandir-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
