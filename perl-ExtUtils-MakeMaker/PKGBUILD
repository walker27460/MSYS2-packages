# Maintainer: Andrea Dalle Vacche

_realname=ExtUtils-MakeMaker
pkgname=perl-${_realname}
pkgver=7.31_08
pkgrel=1
pkgdesc="ExtUtils::MakeMaker - Create a module Makefile"
arch=('any')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/~bingos/ExtUtils-MakeMaker/"
depends=('perl')
groups=('perl-modules')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/B/BI/BINGOS/${_realname}-${pkgver}.tar.gz
        001-msysize.patch)
sha256sums=('309047dce3e808e194a2b3d788e01b627d9ccd307f390a9d68f68ef879b35957'
            '7e3f4c26ab5d0f9dedbdfa7d244946012f72253d12b37301a2394598a60df744')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-msysize.patch
}

build() {
  cd ${_realname}-${pkgver}
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
