# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=YAML-Syck
pkgname=perl-YAML-Syck
pkgver=1.30
pkgrel=2
pkgdesc="Fast, lightweight YAML loader and dumper"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/YAML-Syck/"
license=('BSD' 'custom')
depends=('perl')
groups=('perl-modules')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/T/TO/TODDR/YAML-Syck-${pkgver}.tar.gz
        'msysize.patch')
sha256sums=('62b344216176556a0c497065ad8385157b9f261c6e687218dfa19ec7d7258dc7'
            '808289add8a10652ddf25d25e3053ff9b5619daa92dd923f02bf70b2366d17cd')

prepare() {
  cd YAML-Syck-${pkgver}
  patch -p1 -i ${srcdir}/msysize.patch
}

build() {
  cd YAML-Syck-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd YAML-Syck-${pkgver}
  make test
}

package() {
  cd YAML-Syck-${pkgver}
  make install DESTDIR="${pkgdir}"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  # fix permissons
  find ${pkgdir}/usr -type f \( -name *.dll -o -name *.exe \) | xargs chmod 0755
}
