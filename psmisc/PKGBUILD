# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=psmisc
pkgver=22.21
pkgrel=2
pkgdesc="Miscellaneous procfs tools"
arch=('i686' 'x86_64')
url="https://psmisc.sourceforge.io/"
license=('GPL')
groups=('sys-utils')
depends=('msys2-runtime' 'gcc-libs' 'ncurses' 'libiconv' 'libintl')
makedepends=('gcc' 'ncurses-devel' 'libiconv-devel' 'gettext-devel')
source=(https://downloads.sourceforge.net/psmisc/${pkgname}-${pkgver}.tar.gz
        'psmisc-fuser.patch')
sha256sums=('97323cad619210845b696d7d722c383852b2acb5c49b5b0852c4f29c77a8145a'
            'dc3966a1fb41863602d687a9e13fdd01708ad4c6fc905711380b7265b571d992')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/psmisc-fuser.patch
  autoreconf -fi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --without-libiconv-prefix \
    --without-libintl-prefix \
    CPPFLAGS="${CPPFLAGS} -I/usr/include/ncursesw"
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
