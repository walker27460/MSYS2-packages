# Maintainer: Alexey Pavlov <Alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=doxygen
pkgver=1.8.14
pkgrel=1
pkgdesc="A documentation system for C++, C, Java, IDL and PHP"
arch=('i686' 'x86_64')
url="http://www.doxygen.org/"
options=('strip' 'staticlibs')
license=('GPL')
depends=("gcc-libs" "libsqlite" "libiconv")
makedepends=("cmake" "gcc" "flex" "python2" "libsqlite-devel" "libiconv-devel")
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/doxygen/doxygen/archive/Release_${pkgver//./_}.tar.gz
        "doxygen-segfault.patch")
noextract=("${pkgname}-${pkgver}.tar.gz")
sha256sums=('18bc3790b4d5f4d57cb8ee0a77dd63a52518f3f70d7fdff868a7ce7961a6edc3'
            '220f5446dd2712cb043ced03cb2927596ed25a5e60150499be9213acba5b5c25')

prepare() {
  # workaround for symlink issue
  tar -xf ${pkgname}-${pkgver}.tar.gz || true
  cp ${pkgname}-Release_${pkgver//./_}/addon/doxmlparser/src/doxmlintf.h \
    ${pkgname}-Release_${pkgver//./_}/addon/doxmlparser/include/doxmlintf.h
}

build() {
  local extra_config=Release
  if check_option "debug" "y"; then
    extra_config=Debug
  fi

  mkdir -p build-${CHOST} && cd build-${CHOST}
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=${extra_config} \
    -Dbuild_wizard=OFF \
    -Dbuild_search=OFF \
    -Duse_sqlite3=ON \
    -Duse_libclang=OFF \
    ../${pkgname}-Release_${pkgver//./_}
  make
}

package() {
  cd ${srcdir}/build-${CHOST}

  make DESTDIR="${pkgdir}" install
}
