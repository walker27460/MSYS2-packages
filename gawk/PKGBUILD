# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gawk
pkgver=4.2.1
pkgrel=2
pkgdesc="GNU version of awk"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/gawk/"
license=('GPL')
groups=('base' 'base-devel')
depends=('sh' 'mpfr' 'libintl' 'libreadline')
makedepends=('gettext-devel' 'mpfr-devel' 'libreadline-devel')
provides=('awk')
install=gawk.install
source=(https://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig}
        gawk-4.2.1-msysize.patch
        gawk-4.2.1-autotools.patch
        fix-cr.patch)
sha256sums=('2b23d51503b2df9a41aa6fddc6002ad7ebf2a386ac19dc1b6be0dd48b0acf6db'
            'SKIP'
            '8a91f2c369303ae678d72f9d9d7cd1f102d7b1af521aa3df3132ce631004e1e7'
            '8c52a882e4e9f020c34610a8e1729ce60a688533504b5ac38ac9289a92a42d80'
            'd217538ce260b588854cc5882e3cc9e95c715ae212f016ff27449db637e69093')
validpgpkeys=('D1967C63788713177D861ED7DF597815937EC0D2')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/gawk-4.2.1-msysize.patch
  patch -p1 -i ${srcdir}/gawk-4.2.1-autotools.patch
  patch -p1 -i ${srcdir}/fix-cr.patch
  autoreconf -fiv
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --build=${CHOST} \
              --prefix=/usr \
              --libexecdir=/usr/lib \
              --without-libiconv-prefix \
              --without-libintl-prefix \
              --without-libsigsegv

  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
