# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

pkgbase=meson
pkgname=meson
pkgver=0.48.2
pkgrel=1
pkgdesc='High-productivity build system'
arch=('any')
url="https://mesonbuild.com/"
license=('Apache 2')
depends=('python3' 'python3-setuptools' 'ninja')
source=("https://github.com/mesonbuild/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "cygwin-disguise.patch")
sha256sums=('39ead8bfd0dc9c7b0af15e23ea975c864600bf871fba279c9918625bb9a85506'
            '56fc97ffa548c72b3a5acb165506679eaa173bffccd5d7a061800fd33bbe41c9')

prepare() {
  cd "$srcdir/${pkgname}-$pkgver"

  patch -p1 -i "${srcdir}"/cygwin-disguise.patch
}

build() {
  cd "$srcdir/${pkgname}-$pkgver"

  python3 setup.py build
}

package() {
  cd "$srcdir/${pkgname}-$pkgver"

  python3 setup.py install --root="$pkgdir/" --optimize=1

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
