# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

pkgname=meson
pkgver=0.49.0
pkgrel=1
pkgdesc='High-productivity build system'
arch=('any')
url="https://mesonbuild.com/"
license=('Apache 2')
depends=('python3' 'python3-setuptools' 'ninja')
source=("https://github.com/mesonbuild/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "cygwin-disguise.patch")
sha256sums=('fb0395c4ac208eab381cd1a20571584bdbba176eb562a7efa9cb17cace0e1551'
            'a2cdd9f13e5bcce017e6653b9d765e0bd400493671e7a9e7ecc6203f35abe05b')

prepare() {
  cd "$srcdir/${pkgname}-$pkgver"

  patch -p1 -i "${srcdir}"/cygwin-disguise.patch
}

build() {
  cd "$srcdir/${pkgname}-$pkgver"

  python3 setup.py build
}

package() {
  cd "$srcdir/${pkgname}-$pkgver"

  python3 setup.py install --root="$pkgdir/" --optimize=1

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
