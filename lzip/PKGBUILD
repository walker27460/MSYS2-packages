# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=lzip
pkgver=1.20
pkgrel=1
pkgdesc="A lossless file compressor based on the LZMA algorithm"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/lzip/lzip.html"
license=('GPL3')
depends=('gcc-libs')
install=lzip.install
source=(https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('c93b81a5a7788ef5812423d311345ba5d3bd4f5ebf1f693911e3a13553c1290c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr CXXFLAGS="$CXXFLAGS"
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install{,-man}
}
