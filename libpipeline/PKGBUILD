# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('libpipeline' 'libpipeline-devel')
pkgver=1.5.0
pkgrel=1
pkgdesc="a C library for manipulating pipelines of subprocesses in a flexible and convenient way"
url="http://libpipeline.nongnu.org/"
groups=('libraries')
arch=('i686' 'x86_64')
license=('BSD')
makedepends=('openssl-devel' 'zlib-devel')
options=('!libtool')
source=(https://download.savannah.gnu.org/releases/libpipeline/libpipeline-$pkgver.tar.gz{,.sig}
        libpipeline-1.5.0-gnulib.patch
        libpipeline-1.4.1-msys2.patch)
sha256sums=('0d72e12e4f2afff67fd7b9df0a24d7ba42b5a7c9211ac5b3dcccc5cd8b286f2b'
            'SKIP'
            '0559526eecb02415f210c6c0d2b0a5b5694ffe8637b74840684433f8cdb1bd37'
            'ca3d2b15f0da4b593ca4a6b25b7555a96f3a565d75edf11b93cd97ca1b831265')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/libpipeline-1.5.0-gnulib.patch
  patch -p1 -i ${srcdir}/libpipeline-1.4.1-msys2.patch

  autoreconf -fi
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --build=${CHOST} --prefix=/usr
  make
  make DESTDIR="${srcdir}/dest" install
}

check() {
  cd "${pkgname}-${pkgver}"
  make check
}

package_libpipeline() {
  depends=('gcc-libs')
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libpipeline-devel() {
  pkgdesc="libpipeline headers and libraries"
  options=('staticlibs')
  depends=("libpipeline=${pkgver}")
  groups=('development')

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
